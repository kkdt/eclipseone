/**
 * Copyright (C) 2017 thinh ho
 * This file is part of 'eclipseone' which is released under the MIT license.
 * See LICENSE at the project root directory.
 */

buildscript {
   repositories {
      maven {
         url "https://plugins.gradle.org/m2/"
      }
   }
   dependencies {
      classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
   }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

group = 'kkdt.gradle.eclipseone'
version = '0.5'

repositories {
    jcenter()
}

eclipse {
   classpath {
      downloadJavadoc = true
      downloadSources = true
   }
}

dependencies {
   compile gradleApi()
   compile localGroovy()
}

pluginBundle {
   website = 'https://github.com/kkdt/eclipseone'
   vcsUrl = 'https://github.com/kkdt/eclipseone.git'
   description = 'Single-project Eclipse view for a multi-project build'
   tags = ['eclipse','java']
 
   plugins {
     eclipseOnePlugin {
       id = 'kkdt.gradle.eclipseone'
       displayName = 'Eclipse-One Plugin'
     }
   }
}

publishing {
   publications {
       myPublication(MavenPublication) {
           // publish jar archive
           from components.java
           // include publish README.md
           artifact ('README.md') {
               classifier = 'README'
               extension  = 'md'
           }
       }
   }
   // publish artifacts to local directory
   repositories {
      maven {
         url "file:/" + buildDir.path + "/artifacts"
      }
   }
}

